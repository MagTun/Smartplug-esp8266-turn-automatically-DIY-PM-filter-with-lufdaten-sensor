>D
; declare var. for pm10 and pm25: a string var (to extract the json text), a number var (to convert the string into a num) and a threshold var (to easily change it)
pm10=""
pm10n=0
; default 15
pm10t=15

pm25=""
pm25n=0
; default 5
pm25t=5

; declare var to make the http call 
res=0

; declare var to avoid many print
flg=0


>S
; make the call every x seconds (here it's 60)
if upsecs%60==0
then
flg=1
; get contents to http page, and when ready call section
res=http("192.168.1.39:888" "/data.json")
flg=0
endif

>E

if flg>0
then
; extract 4th/6th occurrence of text "value", eg.  ":"5.22"},{"
pm10=gwr("value" 4)
pm25=gwr("value" 6)

; parse the result (remove  " and keep only numbers)
pm10=st(pm10 " 2)
print PM10  %pm10%
pm25=st(pm25 " 2)
print PM2.5  %pm25%

; convert string to number
pm10n=pm10
pm25n=pm25


if pm25n>pm25t
then 
print Turn ON bcs PM25 >  %pm25t%
=>power1 1

else 

if pm10n>pm10t
then 
print Turn ON bcs PM10 >  %pm10t%
=>power1 1

else 
print PM25 and PM25 < threshold 
; will switch off!
=>power1 0
; endif pm10
endif
;endif pm25
endif
;endif flg
endif
